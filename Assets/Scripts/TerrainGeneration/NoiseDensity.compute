// #include "Assets/Scripts/Compute/Includes/Noise.compute"
#include "Assets/Resources/SimplexNoise2.cginc"

#define FLT_MAX 1e+10
#define FLT_MIN -1e+10
#define FLT_MIN_POS 1e-10

struct GroundLevelData
{
    float weight;
    float twoDimentionalHeight;
    bool hasMeshAtThisPlace;
};

// Noise settings
StructuredBuffer<float3> offsets;
RWStructuredBuffer<float> manualData;
RWStructuredBuffer<GroundLevelData> GroundLevelDataBuffer;
RWStructuredBuffer<float4> points;
RWStructuredBuffer<int> pointsStatus;

int octaves;
float radius;
float lacunarity, persistence, noiseScale, noiseWeight, floorOffset, 
heightGredient, multiFractalWeight;

float4 params;
int numPointsPerAxis;
float boundsSize;
float3 centre;
float3 worldSize;
float spacing;
bool closeEdges;

int indexFromCoord(uint x, uint y, uint z) {
    return z * numPointsPerAxis * numPointsPerAxis + y * numPointsPerAxis + x;
}

float customDistance(float3 pos, float r){
    // return radius - distance(float2(0,0), float2(pos.x, pos.y));
    return -pos.y;
}

float3 hitWorldPos;
float affactRadius;
float affactWeight;
bool processDrawing;

// k0: calculate chunk volume data,
// optional: process drawing
#pragma kernel CalculateChunkVolumeData
[numthreads(8,8,8)]
void CalculateChunkVolumeData (int3 id : SV_DispatchThreadID)
{
    if (id.x >= numPointsPerAxis || id.y >= numPointsPerAxis || id.z >= numPointsPerAxis) return;

    int index = indexFromCoord(id.x,id.y,id.z);
    
    // World pos
    float3 pos = centre + id * spacing - boundsSize/2;

    float noise = 0;

    float frequency = noiseScale * 0.01f;
    float amplitude = 1;
    float weight = 1;

    for (int j = 0; j < octaves; j++) {
        // -1 -> 1
        float3 noiseSamplePoint = pos * frequency + offsets[j];
        noiseSamplePoint.y = 0.0f;
        float n = snoise(noiseSamplePoint);

        n = abs(n);
        n = - n + 1;
        
        if(j >= 1){
            n *= noise * multiFractalWeight;
        }
        noise += n * amplitude;

        amplitude *= persistence;
        frequency *= lacunarity;
    }
    noise *= noiseWeight;

    // The higher the position is, the less likely it is gonna appear.
    // float finalVal = (radius - distance(float3(0,0,0), pos)) * heightGredient + floorOffset + noise;
    float finalVal = -pos.y * heightGredient + floorOffset + noise;

    if(processDrawing){
        manualData[index] += 
        clamp(affactRadius - distance(pos, hitWorldPos), 0, affactRadius) * affactWeight;
    }
    
    // -pos.y * heightGredient + floorOffset + noise + manualData[index] = 0
    float heightOnThisPos = (floorOffset + noise + manualData[index]) / heightGredient;
    float heightOnThisPosWithoutManual = (noise + floorOffset) / heightGredient;

    if (closeEdges) {
        float3 edgeOffset = abs(pos*2)-worldSize + spacing/2;
        float edgeWeight = saturate(sign(max(max(edgeOffset.x,edgeOffset.y),edgeOffset.z)));
        finalVal = finalVal * (1-edgeWeight) - 100 * edgeWeight;
    }

    float finalWeight = finalVal + manualData[index];

    if(finalWeight > 0) pointsStatus[0] = 1;
    else pointsStatus[1] = 1;
    
    GroundLevelDataBuffer[index].weight = finalWeight; // To GrassChunkPoint.compute
    GroundLevelDataBuffer[index].twoDimentionalHeight = FLT_MIN; // Fill default data

    points[index] = float4(pos, finalWeight); // To marchingcubes.compute
}
